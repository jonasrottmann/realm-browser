apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'realm-android'

group = 'de.jonasrottmann.realm-browser'

android {
    compileSdkVersion 23
    buildToolsVersion project.buildToolsVersion
    resourcePrefix 'realm_browser_'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 23
        versionCode project.gitCommitCount
        versionName project.gitTag
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}